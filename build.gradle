plugins {
    id "java"
    id "org.embulk.embulk-plugins" version "0.5.5"
    id "com.palantir.git-version" version "0.13.0"
    id "com.diffplug.spotless" version "5.15.0"
    id "checkstyle"
}

repositories {
    mavenCentral()
}

group = "io.trocco"
description = "Internal Http filter plugin for Embulk."


sourceCompatibility = 1.8
targetCompatibility = 1.8

version = {
    def vd = versionDetails()
    if (vd.commitDistance == 0 && vd.lastTag ==~ /^v[0-9]+\.[0-9]+\.[0-9]+(\.[a-zA-Z0-9]+)?/) {
        vd.lastTag.substring(1)
    } else {
        "0.0.0.${vd.gitHash}"
    }
}()


dependencies {
    def embulkVersion = "0.10.42"
    compileOnly("org.embulk:embulk-api:${embulkVersion}")
    compileOnly("org.embulk:embulk-spi:${embulkVersion}")

    compile "org.embulk:embulk-util-config:0.3.4"
    compile "org.embulk:embulk-util-timestamp:0.3.0"
    compile "org.embulk:embulk-util-json:0.3.0"

    // https://mvnrepository.com/artifact/org.apache.httpcomponents/httpclient
    compile group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.12'

    // compile "YOUR_JAR_DEPENDENCY_GROUP:YOUR_JAR_DEPENDENCY_MODULE:YOUR_JAR_DEPENDENCY_VERSION"
    testImplementation "junit:junit:4.13.2"
    testImplementation "org.mock-server:mockserver-junit-rule:5.11.2"
    testImplementation "org.embulk:embulk-core:${embulkVersion}"
    testImplementation "org.embulk:embulk-deps:${embulkVersion}"
    testImplementation "org.embulk:embulk-junit4:${embulkVersion}"
}

checkstyle {
    configFile = file("${project.rootDir}/config/checkstyle/checkstyle.xml")
    toolVersion = '6.14.1'
}
checkstyleMain {
    configFile = file("${project.rootDir}/config/checkstyle/default.xml")
    ignoreFailures = true
}
checkstyleTest {
    configFile = file("${project.rootDir}/config/checkstyle/default.xml")
    ignoreFailures = true
}
task checkstyle(type: Checkstyle) {
    classpath = sourceSets.main.output + sourceSets.test.output
    source = sourceSets.main.allJava + sourceSets.test.allJava
}

embulkPlugin {
    mainClass = "org.embulk.filter.internal_http.InternalHttpFilterPlugin"
    category = "filter"
    type = "internal_http"
}

tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
    options.encoding = "UTF-8"
}

clean {
    delete "classpath"
}


gem {
    from("LICENSE.txt")
    authors = ["naotaka nakane"]
    email = ["n.nakane0219@gmail.com"]
    summary = "Formats JSONL files for other file output plugins."
    homepage = "https://github.com/n.nakane0219/embulk-filter-internal_http"
    licenses = [ "MIT" ]
}

